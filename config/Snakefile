# shell environment used for each job
# note, this is a naked shell, not aware of your bashrc!
shell.executable("/bin/bash")
# unofficial bash strict mode
shell.prefix("set -euo pipefail;")# don't forget trailing semicolon

done_list=[]
for sample in config['samples'].keys():
    gid=config['samples'][sample]['gid']
    done_list.append("log/reads/{}/{}.done".format(gid,sample))


def sim_done(wildcards):
    return expand("reads/{gid}/{sample}_{index}.fastq.gz", gid=config['samples'][wildcards.sample]['gid'], sample=wildcards.sample, index=range(1,3))


rule all:
    input:
        done_list

rule sim_done:
    input:
        sim_done
    output:
        touch("log/reads/{gid}/{sample}.done")


rule build_index:
    input:
        genome=lambda wildcards: config['genomes'][wildcards.gid],
    params:
        prefix="genome_index/{gid}"
    output:
        expand("genome_index/{{gid}}.{index}.bt2",  index=range(1,5)),
        expand("genome_index/{{gid}}.rev.{index}.bt2", index=range(1,3)),
    log:
        "log/build_index.log"
    shell:
        '''
        bowtie2-build {input.genome} {params.prefix}
        '''


rule align_probe:
    input:
        rules.build_index.output,
        probe = config['probe']
    output:
        bam="mapping/{gid}.sorted.bam",
        bai="mapping/{gid}.sorted.bam.bai",
    params:
        rprefix="genome_index/{gid}",
        bprefix="mapping/{gid}.sorted",
    log:
        "log/align_probe.log"
    shell:
        '''
        bowtie2 --local --very-sensitive-local -p 8 --mp 8 --rdg 10,8 --rfg 10,8 -k 10000 -f -x {params.rprefix} -U {input.probe} | samtools view -bSu - | samtools sort - {params.bprefix}
        samtools index {output.bam}
        '''


rule run_capsim:
    input:
        genome=lambda wildcards: config['genomes'][wildcards.gid],
        bam="mapping/{gid}.sorted.bam",
        bai="mapping/{gid}.sorted.bam.bai",
    params:
        readnum=lambda wildcards: config['samples'][wildcards.sample]['readnum'],
        sprefix="reads/{gid}/{sample}",
    output:
        expand("reads/{{gid}}/{{sample}}_{index}.fastq.gz", index=range(1,3))
    log:
        "log/run_capsim.log"
    shell:
        '''
        jsa.sim.capsim --reference {input.genome} --probe {input.bam} --ID {wildcards.sample} --miseq {params.sprefix} --fmedian {config[fmedian]} --illen {config[rlen]} --num {params.readnum} --seed {config[seed]}
        '''
